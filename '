extends Node

@export_group("Dripstones")

@export_subgroup("Nodes")
@export var scene: PackedScene
@export var destroy_area: Area2D
@export var timer: Timer

@export_subgroup("Spawn Position")
@export var x: float
@export var y_min: float
@export var y_max: float

@export_group("GUI")
@export var gui: Control

var score: int = 0
var dripstones_speed: float = 600.0


func _ready() -> void:
	if scene == null:
		push_warning("The dripstones scene has not been assigned in the editor")

	destroy_area.area_entered.connect(_on_area_entered)
	timer.timeout.connect(_on_timer_timeout)

	timer.start()


func _on_area_entered(area: Area2D) -> void:
	area.queue_free()


func _on_timer_timeout() -> void:
	var dripstones: Node2D = scene.instantiate()

	dripstones.scale = Vector2(10, 10)
	dripstones.position = Vector2(x, randf_range(y_min, y_max))
	dripstones.speed = dripstones_speed
	dripstones.point_scored.connect(_on_point_scored)

	self.add_child(dripstones)


func _on_point_scored() -> void:
	var score_label: Label = gui.get_node("%ScoreLabel")
	score += 1
	score_label.text = str(score)

	dripstones_speed += score * 5

	# Make sure that the existing dripstones have their speed changed too
	for node: Node in get_tree().get_nodes_in_group("dripstones"):
		node.speed = dripstones_speed
